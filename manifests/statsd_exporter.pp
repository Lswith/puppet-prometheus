# Class: prometheus::statsd_exporter
#
# This module manages prometheus node statsd_exporter
#
# Parameters:
#
#  [*manage_user*]
#  Whether to create user for prometheus or rely on external code for that
#
#  [*user*]
#  User running statsd_exporter
#
#  [*manage_group*]
#  Whether to create user for statsd_exporter or rely on external code for that
#
#  [*purge_config_dir*]
#  Purge config files no longer generated by Puppet
#
#  [*mapping_config_path*]
#  The path to the mapping config
#
#  [*statsd_maps*]
#  The hiera array for mappings:
#    - map: 'test.dispatcher.*.*.*'
#      name: 'dispatcher_events_total'
#      labels:
#        processor: '$2'
#        action: '$1'
#
#  [*group*]
#  Group under which statsd_exporter is running
#
#  [*bin_dir*]
#  Directory where binaries are located
#
#  [*arch*]
#  Architecture (amd64 or i386)
#
#  [*version*]
#  Prometheus node statsd_exporter release
#
#  [*install_method*]
#  Installation method: url or package (only url is supported currently)
#
#  [*os*]
#  Operating system (linux is the only one supported)
#
#  [*download_url*]
#  Complete URL corresponding to the Prometheus node statsd_exporter release, default to undef
#
#  [*download_url_base*]
#  Base URL for prometheus node statsd_exporter
#
#  [*download_extension*]
#  Extension of Prometheus node statsd_exporter binaries archive
#
#  [*package_name*]
#  Prometheus node statsd_exporter package name - not available yet
#
#  [*package_ensure*]
#  If package, then use this for package ensure default 'latest'
#
#  [*collectors*]
#  The set of node statsd_exporter collectors
#
#  [*extra_options*]
#  Extra options added to prometheus startup command
#
#  [*service_enable*]
#  Whether to enable or not prometheus node statsd_exporter service from puppet (default true)
#
#  [*service_ensure*]
#  State ensured from prometheus node statsd_exporter service (default 'running')
#
#  [*manage_service*]
#  Should puppet manage the prometheus node statsd_exporter service? (default true)
#
#  [*restart_on_change*]
#  Should puppet restart prometheus node statsd_exporter on configuration change? (default true)
#
#  [*init_style*]
#  Service startup scripts style (e.g. rc, upstart or systemd)
#
# Actions:
#
# Requires: see Modulefile
#
# Sample Usage:
#
class prometheus::statsd_exporter (
  $manage_user          = true,
  $user                 = $::prometheus::params::user,
  $manage_group         = true,
  $purge_config_dir     = true,
  $mapping_config_path  = $::prometheus::params::statsd_exporter_mapping_config_path,
  $statsd_maps          = $::prometheus::params::statsd_exporter_maps,
  $group                = $::prometheus::params::group,
  $bin_dir              = $::prometheus::params::bin_dir,
  $arch                 = $::prometheus::params::arch,
  $version              = $::prometheus::params::statsd_exporter_version,
  $install_method       = $::prometheus::params::install_method,
  $os                   = $::prometheus::params::os,
  $download_url         = undef,
  $extra_groups         = $::prometheus::params::statsd_exporter_extra_groups,
  $download_url_base    = $::prometheus::params::statsd_exporter_download_url_base,
  $download_extension   = $::prometheus::params::statsd_exporter_download_extension,
  $package_name         = $::prometheus::params::statsd_exporter_package_name,
  $package_ensure       = $::prometheus::params::statsd_exporter_package_ensure,
  $extra_options        = '',
  $config_mode          = $::prometheus::params::config_mode,
  $service_enable       = true,
  $service_ensure       = 'running',
  $manage_service       = true,
  $restart_on_change    = true,
  $init_style           = $::prometheus::params::init_style,
) inherits prometheus::params {
  $real_download_url    = pick($download_url,"${download_url_base}/download/${version}/${package_name}-${version}.${os}-${arch}.${download_extension}")
  validate_bool($purge_config_dir)
  validate_bool($manage_user)
  validate_bool($manage_service)
  validate_bool($restart_on_change)
  $notify_service = $restart_on_change ? {
    true    => Service['statsd_exporter'],
    default => undef,
  }

  $extra_statsd_maps = hiera_array('prometheus::statsd_exporter::statsd_maps')
  $real_statsd_maps = concat($extra_statsd_maps, $prometheus::statsd_exporter::statsd_maps)

  file { $mapping_config_path:
    ensure  => 'file',
    mode    => '0644',
    owner   => $user,
    group   => $group,
    content => template('prometheus/statsd_mapping.conf.erb'),
    notify  => $notify_service,
  }

  $options = "-statsd.mapping-config=\'${prometheus::statsd_exporter::mapping_config_path}\' ${prometheus::statsd_exporter::extra_options}"
  $daemon_name = 'statsd_exporter'

  prometheus::daemon { 'statsd_exporter':
    install_method     => $install_method,
    daemon_name        => $daemon_name,
    version            => $version,
    download_extension => $download_extension,
    os                 => $os,
    arch               => $arch,
    real_download_url  => $real_download_url,
    bin_dir            => $bin_dir,
    notify_service     => $notify_service,
    package_name       => $package_name,
    package_ensure     => $package_ensure,
    manage_user        => $manage_user,
    user               => $user,
    extra_groups       => $extra_groups,
    group              => $group,
    manage_group       => $manage_group,
    purge              => $purge_config_dir,
    options            => $options,
    init_style         => $init_style,
    service_ensure     => $service_ensure,
    service_enable     => $service_enable,
    manage_service     => $manage_service,
  }
}
